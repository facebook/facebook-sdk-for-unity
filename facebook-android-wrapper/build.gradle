apply plugin: 'com.android.library'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
        maven {
          url "https://maven.google.com"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
    }
}

def fbSdkVersion = project.hasProperty("sdkVersion") ? sdkVersion : '11.0.0'

repositories {
    // If we are doing a local build use our local dir
    if (project.hasProperty("localRepo")) {
        flatDir {
            dirs localRepo
        }
    } else {
      mavenCentral()
      google()
      jcenter()
      maven {
        url "https://maven.google.com"
      }
    }
}

dependencies {
    // Use local file instead of maven to enable testing with versions
    // not released to maven.
    if (project.hasProperty("localRepo")) {
        implementation 'com.facebook.android:facebook-android-sdk:' + fbSdkVersion + '@aar'
        implementation 'com.android.support:support-v4:25.0.0'
        implementation 'com.android.support:cardview-v7:25.0.0'
        implementation 'com.android.support:customtabs:25.0.0'
        implementation 'com.parse.bolts:bolts-android:1.8.4'
    } else {
        implementation 'com.facebook.android:facebook-android-sdk:[11.0.0, 12)'
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}
