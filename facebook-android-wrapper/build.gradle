apply plugin: 'com.android.library'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven {
          url "https://maven.google.com"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}

def fbSdkVersion = project.hasProperty("sdkVersion") ? sdkVersion : '4.33.0'

repositories {
    // If we are doing a local build use our local dir
    if (project.hasProperty("localRepo")) {
        flatDir {
            dirs localRepo
        }
    } else {
      mavenCentral()
      maven {
        url "https://maven.google.com"
      }
    }
}

dependencies {
    // Use local file instead of maven to enable testing with versions
    // not released to maven.
    if (project.hasProperty("localRepo")) {
        compile 'com.facebook.android:facebook-android-sdk:' + fbSdkVersion + '@aar'
        compile 'com.android.support:support-v4:25.0.0'
        compile 'com.android.support:cardview-v7:25.0.0'
        compile 'com.android.support:customtabs:25.0.0'
        compile 'com.parse.bolts:bolts-android:1.8.4'
    } else {
        compile 'com.facebook.android:facebook-android-sdk:' + fbSdkVersion
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '23.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}
